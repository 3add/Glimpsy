plugins {
    id('java')
    id("xyz.jpenilla.run-paper") version("2.3.1")
    id("com.gradleup.shadow") version("9.2.2")
    id("io.papermc.paperweight.userdev") version("2.0.0-beta.19")
}

group = 'threeadd'
version = '1.0'

repositories {
    mavenCentral()

    maven { // paper (obv)
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }

    maven { // fastasyncworldedit, bukkit adapter
        name = "engine-hub-repo"
        url = "https://maven.enginehub.org/repo/"
    }

    maven { // votifier
        name = "jitpack"
        url = "https://jitpack.io"
    }

    maven { // packetevents
        name = "code-mc"
        url = uri("https://repo.codemc.io/repository/maven-releases/")
    }
}

dependencies {
    paperweight.paperDevBundle("1.21.10-R0.1-SNAPSHOT")
    implementation("com.zaxxer:HikariCP:7.0.2")

    implementation(platform("com.intellectualsites.bom:bom-newest:1.55"))
    compileOnly("com.fastasyncworldedit:FastAsyncWorldEdit-Core")
    compileOnly("com.fastasyncworldedit:FastAsyncWorldEdit-Bukkit")
    compileOnly("com.github.NuVotifier:NuVotifier:2.7.2")
    compileOnly("com.github.retrooper:packetevents-spigot:2.10.1")
}

build {
    dependsOn(shadowJar)
}

shadowJar {
    archiveClassifier.set('')
    relocate('com.zaxxer.hikari', 'threeadd.glimpsy.hikari')
    // Glimpsy also uses dependencies in paper by default:

    // - SnakeYaml (articles, loading/saving yml files)
    // - Adventure (a ton, mainly text components)
    // - GSon (better organisation in the DataBase as json data strings)
    // - SLf4J and it's api (logging)
    // - brigadier (commands)
    // - JetBrains annotations
    // - intellectualsites bom (FAWE API)
    // - FAWE API (map reset)
    // - ... (a lot more)

    // Most of these aren't very rare and are almost included in all mc server software, if you ever want to migrate
    // the entirety of glimpsy to a different platform you'd probably have to spend a lot of time doing so since it
    // was specifically designed for paper with these libraries
}

tasks {
    runServer {
        minecraftVersion("1.21")
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset = 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
